import org.apache.tools.ant.filters.ReplaceTokens

buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:1.2.4"
    }
}

group 'cloud.swiftnode.ksecurity'
version '4.0-SNAPSHOT'

apply plugin: 'java'
apply plugin: "com.github.johnrengelman.shadow"

test.testLogging.showStandardStreams = true
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
compileJava.dependsOn clean

sourceCompatibility = 1.8

repositories {
    /* Spigot Repo */
    maven { url 'https://hub.spigotmc.org/nexus/content/groups/public/' }
    /* Bungeecord Repo */
    maven { url 'https://oss.sonatype.org/content/groups/public' }
    /* ProtocolLib */
    maven { url 'http://repo.dmulloy2.net/content/groups/public/' }
    /* bStats */
    maven { url 'http://repo.bstats.org/content/repositories/releases/'}
    mavenCentral()
}

dependencies {
    compile 'org.spigotmc:spigot-api:1.12.2-R0.1-SNAPSHOT'
//    compile 'org.ow2.asm:asm:5.2'
    compile group: 'com.comphenix.protocol', name: 'ProtocolLib', version: '4.4.0-SNAPSHOT'
    compile group: 'org.bstats', name: 'bstats-bukkit', version: '1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.7.0'
}

processResources {
    filteringCharset = 'UTF-8'
    filter ReplaceTokens, tokens: [
            "version"   : project.version,
            "pluginName": rootProject.name,
            "mainClass" : "kr.rvs.ksecurity.KSecurity",
            "author"    : "EntryPoint"
    ]
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.3'
}

shadowJar {
    classifier = ''
    dependencies {
        include dependency('org.bstats:bstats-bukkit')
    }
    relocate 'org.bstats.bukkit', 'kr.rvs.mclibrary.libs.metrics'
}

artifacts {
    version = ''
    archives shadowJar
}
